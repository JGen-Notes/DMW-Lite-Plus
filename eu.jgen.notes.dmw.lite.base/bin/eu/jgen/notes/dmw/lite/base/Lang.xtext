/**
 * [The "BSD license"]
 * Copyright (c) 2016, JGen Notes
 * All rights reserved.
 * 
 * Redistribution and use in source and binary forms, with or without modification, 
 * are permitted provided that the following conditions are met:
 * 
 * 1. Redistributions of source code must retain the above copyright notice, this list of conditions 
 *    and the following disclaimer.
 * 
 * 2. Redistributions in binary form must reproduce the above copyright notice, this list of conditions 
 *    and the following disclaimer in the documentation and/or other materials provided with the distribution.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, 
 * INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE 
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, 
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS 
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, 
 * WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE 
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 * 
 */
 
grammar eu.jgen.notes.dmw.lite.base.Lang with eu.jgen.notes.dmw.lite.mdl.Model

import "http://www.jgen.eu/notes/dmw/lite/mdl/Model" as model

generate lang "http://www.jgen.eu/notes/dmw/lite/base/Lang"

YWidget:
	'package' name=QualifiedName ';'?
	imports+=YImport*
	enums+=YEnumeration*
	classes+=YClass*;

YParameter:
	YTypedDeclaration | YJoinDef;

enum YAccessLevel:
	PRIVATE='private' | PROTECTED='protected' | PUBLIC='public';

YArgument:
	name=ValidID ':' value=YArgumentValue;

YArgumentValue:
	INT | STRING;
	
YEnumeration:
	{YEnumeration} 'enum' name=ValidID (':' superclass=[YEnumeration|QualifiedName])? '{'
	(cases+=YEnumerationCase)* 
	'}'
;

YEnumerationCase:
	{YEnumerationCase}
	'case' name=ValidID ('(' (params+=YParameter (',' params+=YParameter)*)? ')')?
; 

YClass:
	{YClass} 'class' name=ValidID (':' superclass=[YClass|QualifiedName])? ('=>' entityRef=[model::YAnnotEntity|QualifiedName])?
	'{'
	inners+=YClass*
	members+=YMember*
	'}';

YMember:
	YProperty | YFunction;

fragment YTypedDeclaration *:
	name=ValidID ':' type=[YClass|QualifiedName];

YProperty:
	access=YAccessLevel? 'var' YTypedDeclaration tuples=YTuples? (optional?='?')? ('=>' attrRef=[model::YAnnotAttribute|QualifiedName])?
    ';';

YTuples:
	{YTuples} '<' (includes+=[YProperty] (',' includes+=[YProperty])*)? '>';

YFunction:
	access=YAccessLevel? 'func' name=ValidID
	'(' (params+=YParameter (',' params+=YParameter)*)? ')' throw?='throws' (returnvalue?='->')? type=[YClass|QualifiedName]?
	body=YBlock;

YBlock:   
	{YBlock} '{' statements+=YStatement* '}';

YStatement:
	YVariableDeclaration |
	YReturn |
	YReadStatement |
	YCreateStatement |
	YUpdateStatement |
	YAssociateStatement |
	YDisassociateStatement |
	YDeleteStatement |
	YReadEachStatement |
	YWhileStatement |
	YRepeatWhileStatement |
	YForInStatement |
	YExpression ';'? |
	YIfStatement |
	YThrow |
	YDoStatement |
	YSwitchStatement;

YDoStatement:
	{YDoStatement} 'do'  
	block=YBlock	 
	(catches+=YCatch)*
;

YCatch:
	{YCatch} 'catch' exception=[YEnumerationCase|QualifiedName] 
	catchBlock=YCatchBlock	
;

YCatchBlock:   
	{YCatchBlock} '{' statements+=YStatement* '}';

YVariableDeclaration:
	YTypedDeclaration '=' expression=YOrExpression ';';
	
YThrow:
	{YThrow} 'throw' exception=[YEnumerationCase|QualifiedName] 
	'('(arguments+=YOrExpression (',' arguments+=YOrExpression)*)?')'
	';'?
	;

YReturn:
	{YReturn} 'return' expression=YOrExpression? ';';

YIfStatement:
	'if' '(' expression=YOrExpression ')' thenBlock=YBlock
	(=> 'else' elseBlock=YBlock)?;

YSwitchStatement:
	{YSwitchStatement}
	'switch' switchExpression=YExpression '{'
	(cases+=YSwitchCase)*
	('default' ':' default=YBlock)?
	'}';

YSwitchCase:
	{YSwitchCase}
	('case' caseExpression=YExpression)?
	':' then=YBlock;

YSymbol:
	YVariableDeclaration | YParameter;

YNamedElement:
	YClass | YMember | YSymbol;

YExpression:
	YAssignment;

YAssignment returns YExpression:
	YSelectionExpression
	({YAssignment.left=current} '=' right=YOrExpression)?;

YSelectionExpression returns YExpression:
	YTerminalExpression
	({YMemberSelection.receiver=current} '.'
	member=[YMember]
	(functioninvocation?='('
	(args+=YOrExpression (',' args+=YOrExpression)*)? ')')?)*;

/*
 * Expressions
 */
OpSingleAssign:
	'=';

YOrExpression returns YExpression:
	YAndExpression ({YOrExpression.left=current} OpOr right=YAndExpression)*;

OpOr:
	'||';

YAndExpression returns YExpression:
	YEqualityExpression ({YAndExpression.left=current} OpAnd right=YEqualityExpression)*;

OpAnd:
	'&&';

YEqualityExpression returns YExpression:
	YComparisonExpression ({YEqualityExpression.left=current} op=("==" | "!=") right=YComparisonExpression)*;

YComparisonExpression returns YExpression:
	YAdditiveExpression ({YComparisonExpression.left=current} op=OpCompare right=YAdditiveExpression)*;

OpCompare:
	'>=' | '<=' | '>' | '<';

YAdditiveExpression returns YExpression:
	YMultiplicativeExpression (({YPlus.left=current} '+' | {YMinus.left=current} '-') right=YMultiplicativeExpression)*;

OpAdd:
	'+' | '-';

YMultiplicativeExpression returns YExpression:
	YPrimary ({YMulOrDiv.left=current} op=('*' | '/') right=YPrimary)*;

YPrimary returns YExpression:
	{YParenties}'(' a=YOrExpression ')' |
	{YNot} "!" expression=YPrimary |
	YSelectionExpression;

YTerminalExpression returns YExpression:
	{YStringConstant} value=STRING |
	{YIntConstant} value=INT |
	{YBoolConstant} value=('true' | 'false') |
	{YSelf} 'self' |
	{YSuper} 'super' |
	{YNull} 'null' |
	{YSymbolRef} symbol=[YSymbol] |
	{YNew} 'new' type=[YClass|QualifiedName] '('
	(arguments+=YOrExpression (',' arguments+=YOrExpression)*)? ')' 
	//|	'(' YOrExpression ')'
;

YReadStatement:
	'db-read' structs+=YStructRefPair (',' structs+=YStructRefPair)* joinclause=YJoin? whereclause=YWhere?
	;

YReadEachStatement:
	'db-read-each' structs+=YStructRefPair (',' structs+=YStructRefPair)* joinclause=YJoin? whereclause=YWhere?
	'target' target=[YProperty]	
	;

YCreateStatement:
	'db-create' struct=YStructRefPair
	'set' setBlock=YBlock
	;

YUpdateStatement:
	'db-update' struct=YStructRefPair
	'set' setBlock=YBlock
	;

YDeleteStatement:
	'db-delete' struct=YStructRefPair ';';

YAssociateStatement:
	'db-associate' struct=YStructRefPair 'with' joinref=YJoinDef ';';
	

YDisassociateStatement:
	'db-disassociate' struct=YStructRefPair 'from' joinref=YJoinDef ';';

YStructRefPair:
	structproperty=[YProperty] '=>' structclass=[model::YAnnotEntity];

YJoin:
	{YJoin} 'join' joindefs+=YJoinDef ("," joindefs+=YJoinDef)*;

YJoinDef:
	fromView=[YProperty] '=>' relRef=[model::YAnnotRelationship|QualifiedName] '=>' toView=[YProperty];

YWhere:
	{YWhere} 'where' expression=YOrExpression;

YWhileStatement:
	'while' '('expression=YOrExpression')'
	body=YBlock;

YRepeatWhileStatement:
	'repeat'
	body=YBlock
	'while' '(' expression=YOrExpression ')';

YForInStatement:
	'for' item=[YProperty] 'in' collection=[YProperty]
	body=YBlock;
